	;
GOTIT:	CALL CLSCR
	;
	MOV #TITDAT,R0
	MOV #43400,R1
	CALL UNPK
	;
	MOV #100,@#177660
	TST @#177662

	MOV #4,(PC)+
MNUITM:	.WORD 0	; 4,3,2,1

MNULP:	;
	CALL DRMENU
	;
0$:	TSTB @#177660
	BPL 0$
	;
	MOV @#177662,R0
	MOV MNUITM,R1
	CMP R0,#32 ; UP
	BNE 1$
	CMP R1,#4
	BEQ MNULP
	INC R1
	MOV R1,MNUITM
	BR MNULP
	;
1$:	CMP R0,#33 ; DOWN
	BNE 2$
	CMP R1,#1
	BEQ MNULP
	DEC R1
	MOV R1,MNUITM
	BR MNULP
	;
2$:	CMP R0,#12 ; ENTER
	BNE MNULP

	DEC R1
	BEQ 3$ ; EXIT
	DEC R1
	BEQ 4$ ; INFO
	DEC R1
	BEQ 5$ ; ARENA
	; GAME
	TST @#177662
	CLR @#177660
	CALL CLSCR
	CALL GO
	BR GOTIT
	; EXIT
3$:	EMT 14
	RET
	; INFO
4$:	MOV #TX2,R5
	CALL DRINFO
	MOV #TX3,R5
	CALL DRINFO
	MOV #TX4,R5
	CALL DRINFO
	JMP GOTIT
	; ARENA
5$:	INC AMAP
	CMP AMAP,#5
	BLO MNULP
	CLR AMAP
	JMP MNULP

CLSCR:	MOV #VSCR,R0
1$:	CLR (R0)+
	TST R0
	BPL 1$
	RET

DRINFO:	CALL CLSCR
	MOV (R5)+,R1
	MOV R1,R3
	MOV (R5)+,R2
	EMT 24
	MOV (R5)+,R0
1$:	MOV (R5)+,R1
	CLR R2
	EMT 20
	EMT 26
	MOV R3,R1
	EMT 24
	SOB R0,1$
2$:	TSTB @#177660
	BPL 2$
	TST @#177662
	RET

DRMENU:	MOV AMAP,R5
	ADD #61,R5
	MOVB R5,NUMT
	MOV #TX1,R5
	MOV (R5)+,R1
	MOV R1,R3
	MOV (R5)+,R2
	MOV R2,R4
	EMT 24
	MOV (R5)+,R0
1$:	CMP MNUITM,R0
	BNE 2$
	MOV #INVT,R1
	CLR R2
	EMT 20
2$:	MOV (R5)+,R1
	CLR R2
	EMT 20
	CMP MNUITM,R0
	BNE 3$
	MOV #INVT,R1
	CLR R2
	EMT 20
3$:	INC R4
	INC R4
	MOV R3,R1
	MOV R4,R2
	EMT 24
	SOB R0,1$
	RET

TX1:	.WORD 7.,13.
	.WORD 4,STR1,STR2,STR3,STR4
STR1:	.ASCII<40>/START GAME/<40><40><40><40><40><40><40><0>
STR2:	.ASCII<40>/SELECTED ARENA/<40>
NUMT:	.ASCII/1/<40><0>
STR3:	.ASCII<40>/INFORMATION/<40><40><40><40><40><40><0>
STR4:	.ASCII<40>/EXIT/<40><40><40><40><40><40>
	.ASCII<40><40><40><40><40><40><40><0>
INVT:	.ASCII<234><0>
	.EVEN

TX2:	.WORD 4,3
	.WORD 10.,STR5,STR6,STR7,STR8,STR9
	.WORD STR10,STR11,STR12,STR34,STR35
STR5:	.ASCII<237>/LEGEND/<237><12><12><0>
STR6:	.ASCII/MONSTERS	FROM THE PAST/<12><0>
STR7:	.ASCII/FOUND MAGIC PORTAL ON/<12><0>
STR8:	.ASCII/BK 0010 HOME COMPUTERS!/<12><12><0>
STR9:	.ASCII/YOU ARE OUR LAST	HOPE!/<12><12><0>
STR10:	.ASCII/FIGHT WITH THIS EVIL/<12><0>
STR11:	.ASCII/AND WILL	BE THE FORCE/<12><0>
STR12:	.ASCII/WITH YOU./<12><12><0>
STR34:	.ASCII/YOUR TASK IS TO SCORE/<12><0>
STR35:	.ASCII/25 POINTS TO WIN!/<0>
	.EVEN

TX3:	.WORD 3,3
	.WORD 13.,STR13,STR14,STR15,STR16,STR17,STR18
	.WORD STR19,STR20,STR21,STR22,STR23,STR24,STR25
STR13:	.ASCII<237>/CONTROLS/<237><12><12><0>
STR14:	.ASCII/SPACE - FIRE/<12><0>
STR15:	.ASCII/±,Ύ - TURN LEFT,RIGHT/<12><0>
STR16:	.ASCII/³,­,C,Y - FORWARD,BACKWARD/<12><0>
STR17:	.ASCII/J,ας2+³ - TURN AROUND/<12><0>
STR18:	.ASCII/F,W,ας2+±,ας2+Ύ - SLIDE/<12><0>
STR19:	.ASCII/λτ - EXIT/<12><0>
STR20:	.ASCII/σβς - PAUSE/<12><0>
STR21:	.ASCII/ENTER - RESTART/<12><0>
STR22:	.ASCII/ταβ - MAP/<12><12><0>
STR23:	.ASCII/YOU CAN USE JOYSTICK FOR/<12><0>
STR24:	.ASCII/FORWARD,BACKWARD,LEFT,RIGHT/<12><0>
STR25:	.ASCII/FIRE,MAP,RESTART/<0>
	.EVEN

TX4:	.WORD 4,3
	.WORD 8.,STR26,STR27,STR28,STR29,STR30,STR31
	.WORD STR32,STR33
STR26:	.ASCII<237>/AUTHORS/<237><12><12><0>
STR27:	.ASCII/ENGINE,GAME:/<12><0>
STR28:	.ASCII/APRAKSIN	DMITRY (ADW2RU)/<12><12><0>
STR29:	.ASCII/TITLE PICTURE:/<12><0>
STR30:	.ASCII/LASHIN URY (InoArt Studio)/<12><12><0>
STR31:	.ASCII/SUPPORT BK COMMUNITY:/<12><0>
STR32:	.ASCII/t.me/<57>/bk0010_11m/<12><12><0>
STR33:	.ASCII/2024 YEAR/<0>
	.EVEN

	; NRV2d	(UCL) FOR BK/PDP
	; INPUT:
	; R0 - SOURCE
	; R1 - DESTINATION

UNPK:	MOV #-1,R4
	XOR R2,R2
	BR DCL

NXDC:	MOVB (R0)+,(R1)+
DCL:	CALL RB
	BCS NXDC

	MOV #1,R3
OFN:	ADD R3,R3
	CALL RB
	ADC R3
	CALL RB
	BCS OFE
	DEC R3
	ADD R3,R3
	CALL RB
	ADC R3
	BR OFN

RB:	ADD R2,R2
	BEQ RBZ
	RET
RBZ:	MOVB (R0)+,R2
	SWAB R2
	BIC #177,R2
	BIS #200,R2
	ADD R2,R2
	RET

OFE:	SUB #3,R3
	BCC OF2

	MOV R4,R3
	XOR R5,R5
	CALL RB
	ADC R5
	BR OF3

OF2:	SWAB R3
	MOVB (R0)+,R5
	BIC #177400,R5
	BIS R5,R3
	COM R3
	BEQ BRK
	XOR R5,R5
	SEC
	ROR R3
	ADC R5
	MOV R3,R4

OF3:	ADD R5,R5
	CALL RB
	ADC R5
	BNE LF1

	INC R5
NXLF:	ADD R5,R5
	CALL RB
	ADC R5
	CALL RB
	BCC NXLF
	ADD #2,R5

LF1:	CMP R3,#175400
	ADC R5
	INC R5

	ADD R1,R3
NXMV:	MOVB (R3)+,(R1)+
	SOB R5,NXMV
	BR DCL

BRK:	RET

	.END
